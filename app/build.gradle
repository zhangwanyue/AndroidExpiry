apply plugin: 'com.android.application'

def getStdout = { command, defaultValue ->
    def stdout = new ByteArrayOutputStream()
    try {
        exec {
            commandLine = command
            standardOutput = stdout
        }
        return stdout.toString().trim()
    } catch (Exception ignored) {
        return defaultValue
    }
}

android {
    compileSdkVersion 26
    defaultConfig {
        applicationId "com.example.vera.expirytest"
        minSdkVersion 15
        targetSdkVersion 26
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        def now = (long) (System.currentTimeMillis() / 1000)
        //use git committer date(UNIX timestamp) to set buildTimestamp
        buildConfigField "Long", "BuildTimestamp",
                "${getStdout(['git', 'log', '-n', '1', '--format=%ct'], now)}000L"
        //set app_name
        resValue "string", "app_name", "Briar"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug{
            //add build config field, find in app/build/generated/source/buildConfig/debug/com/example/vera/gradlebuildconfig/BuildConfig.java
            applicationIdSuffix ".debug"
            //set debug app_name(use different name for debug and release,这样可以同时安装两个版本)
            resValue "string", "app_name", "Briar Debug"
        }
    }
}

dependencies {
    //explicitly define the conflicted library
    implementation 'com.android.support:support-annotations:27.1.1'
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'com.android.support:appcompat-v7:26.1.0'
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
}
